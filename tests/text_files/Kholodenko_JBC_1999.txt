EGF binds EGFR --> Ra | kf=0.003, kr=0.06 | EGFR=100
Ra dimerizes --> R2 | kf=0.01, kr=0.1
R2 is phosphorylated --> pR2 | kf=1, kr=0.01
pR2 is dephosphorylated --> R2 | V=450, K=50
pR2 binds PLCg --> RPL | kf=0.06, kr=0.2 | PLCg=105
RPL is phosphorylated --> pRPL | kf=1, kr=0.05
pRPL is dissociated into pR2 and pPLCg | kf=0.3, kr=0.006
pPLCg is dephosphorylated --> PLCg | V=1, K=100
pR2 binds Grb2 --> RG | kf=0.003, kr=0.05 | Grb2=85
RG binds SOS --> RGS | kf=0.01, 0.06 | SOS=34
RGS is dissociated into pR2 and GS | kf=0.03, kr=4.5e-3
GS is dissociated into Grb2 and SOS | kf=1.5e-3, kr=1e-4
pR2 binds Shc --> RSh | kf=0.09, kr=0.6 | Shc=150
RSh is phosphorylated --> pRSh | kf=6, kr=0.06
pRSh is dissociated into pShc and pR2 | kf=0.3, kr=9e-4
pShc is dephosphorylated --> Shc | V=1.7, K=340
pRSh binds Grb2 --> RShG | kf=0.003, kr=0.1
RShG is dissociated into pR2 and ShG | kf=0.3, kr=9e-4
RShG binds SOS --> RShGS | kf=0.01, kr=2.14e-2
RShGS is dissociated into ShGS and pR2 | kf=0.12, kr=2.4e-4
pShc binds Grb2 --> ShG | kf=0.003, kr=0.1
ShG binds SOS --> ShGS | kf=0.03, kr=0.064
ShGS is dissociated into pShc and GS | kf=0.1, kr=0.021
pRSh binds GS --> RShGS | kf=0.009, kr=4.29e-2
pPLCg is translocated to cytoskeletal or membrane structures --> pPLCg_I | kf=1, kr=0.03

# Kholodenko, B. N., Demin, O. V, Moehren, G. & Hoek, J. B.
# Quantification of short term signaling by the epidermal growth factor receptor.
# J. Biol. Chem. 274, 30169â€“30181 (1999). https://doi.org/10.1074/jbc.274.42.30169

# observable
# p: parameters
# u: species
@obs Total_phosphorylated_Shc: u[pRSh] + u[RShG] + u[RShGS] + u[pShc] + u[ShG] + u[ShGS]
@obs Total_Grb2_coprecipitated_with_Shc: u[RShG] + u[ShG] + u[RShGS] + u[ShGS]
@obs Total_phosphorylated_Shc_bound_to_EGFR: u[pRSh] + u[RShG] + u[RShGS]
@obs Total_Grb2_bound_to_EGFR: u[RG] + u[RGS] + u[RShG] + u[RShGS]
@obs Total_SOS_bound_to_EGFR: u[RGS] + u[RShGS]
@obs ShGS_complex: u[ShGS]
@obs Total_phosphorylated_PLCg: u[pRPL] + u[pPLCg]

# simulation
@sim tspan: [0, 120]
@sim condition EGF20nM: init[EGF] = 680
@sim condition EGF2nM: init[EGF] = 68